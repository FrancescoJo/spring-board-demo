apply plugin: "groovy"
apply plugin: "org.asciidoctor.convert"

apply from: "$project.rootDir/gradle/scripts/java.gradle"
apply from: "$project.rootDir/gradle/scripts/kotlin.gradle"
apply from: "$project.rootDir/app-main/versioning.gradle"

def groovyVersion = "2.5.3"
def spockVersion = "1.3-groovy-2.5"

sourceSets {
    integrationTest {
        final srcBase = "src/integrationTest"
        java {
            srcDirs = [srcDirs, "$srcBase/groovy", "$srcBase/java"]
            compileClasspath += main.output + test.output
            runtimeClasspath += main.output + test.output
        }

        // !chore: Gradle 7.0 warning and must be removed
        resources {
            srcDirs = [srcDirs, "$srcBase/resources"]
        }
    }
}

idea {
    module {
        testSourceDirs += project.sourceSets.integrationTest.java.srcDirs
        testResourceDirs += project.sourceSets.integrationTest.resources.srcDirs
        // !chore: Gradle 7.0 warning
        scopes.TEST.plus += [configurations.integrationTestCompile]
    }
}

configurations {
    integrationTestCompile.extendsFrom testCompile
    integrationTestRuntime.extendsFrom testRuntime
}

task integrationTest(type: Test) {
    description "Runs the integration tests."
    group "verification"

    testClassesDirs = sourceSets.integrationTest.output.classesDirs
    classpath = sourceSets.integrationTest.runtimeClasspath
    outputs.upToDateWhen { false }
    mustRunAfter test
    finalizedBy asciidoctor
}

check.dependsOn integrationTest

dependencies {
    integrationTestImplementation project(":app-main")

    // lang
    integrationTestImplementation "org.codehaus.groovy:groovy:$groovyVersion"

    // Spring boot test libs
    integrationTestImplementation "org.springframework.boot:spring-boot-starter-test:$version_springBoot"
    integrationTestImplementation "org.springframework.restdocs:spring-restdocs-core:$version_restDocs"
    integrationTestImplementation "org.springframework.restdocs:spring-restdocs-restassured:$version_restDocs"
    integrationTestImplementation "org.springframework.restdocs:spring-restdocs-asciidoctor:$version_restDocs"

    // spock framework
    integrationTestImplementation "org.spockframework:spock-core:$spockVersion"
    integrationTestImplementation "org.spockframework:spock-spring:$spockVersion"

    // in-memory database
    integrationTestImplementation "com.h2database:h2:$version_h2db"

    // spock mock support
    integrationTestRuntimeOnly "cglib:cglib-nodep:3.2.4"
    integrationTestRuntimeOnly "net.bytebuddy:byte-buddy:1.6.5"
    integrationTestRuntimeOnly "org.objenesis:objenesis:2.5.1"

    integrationTestImplementation project(path: ":lib-common", configuration: "testArtifacts")
    integrationTestImplementation project(path: ":app-main", configuration: "testArtifacts")
}

asciidoctor {
    inputs.dir "${project.buildDir}/generated-snippets"
    /*
     * In cases of this option is ineffective under Windows environment, you should set global environment variables
     * via "Control Panel" > "[Advanced]" > "Environment variables..." and put values as following:
     *
     * set JAVA_OPTS=-Dfile.encoding=UTF-8
     * set GRADLE_OPTS=-Dfile.encoding=UTF-8
     *
     * and reboot system to apply values.
     */
    options.encoding = "UTF-8"
}

// https://stackoverflow.com/questions/50681096/groovy-2-5-0-gives-noclassdeffounderror-for-methodcalltransformation
ext["groovy.version"] = "$groovyVersion"
